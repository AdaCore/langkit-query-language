# Flag each reference to infix "=" and "/=" predefined operators when
# one of its operand is one of the names provided as a parameter.
# Parameters should be fully qualified names.

import stdlib

fun match_name(name, actuals) =
    name is Name(
        p_referenced_defining_name() is DefiningName(
            p_canonical_fully_qualified_name() is str@*)) when
    [s for s in actuals if str == s.to_lower_case]

@check(message="direct (in)equality",
       category="Style", subcategory="Programming Practice")
fun direct_equalities(node, actuals = []) =
    node is BinOp(f_op is op@(OpEq or OpNeq))
    when stdlib.is_predefined_op(op)
     and match_name(node.f_left, actuals) or match_name(node.f_right, actuals)
