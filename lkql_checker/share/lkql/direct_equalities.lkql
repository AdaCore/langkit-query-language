# Flag infix calls to the predefined "=" and "/=" operators when one of
# the operands is the name of a data object provided as a rule parameter.
# Rule parameters should be full expanded Ada names of data objects
# declared by object declaration, number declaration, parameter
# specification, generic object declaration or object renaming
# declaration. Any other parameter does not have any effect.

import stdlib

fun match_name(name, actuals) =
    name is Name(
        p_referenced_defining_name() is DefiningName(
            p_basic_decl() is ObjectDecl | NumberDecl |
                              ParamSpec | GenericFormalObjDecl,
            p_canonical_fully_qualified_name() is str)) when
    [s for s in actuals if str == s.to_lower_case]

@check(message="direct (in)equality",
       category="Style", subcategory="Programming Practice")
fun direct_equalities(node, actuals = []) =
    node is BinOp(f_op is op@(OpEq | OpNeq))
    when stdlib.is_predefined_op(op)
     and match_name(node.f_left, actuals) or match_name(node.f_right, actuals)
