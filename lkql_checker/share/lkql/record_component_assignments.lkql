# Flag each set of assignment to record components when all components of
# the record are assigned, unless the record type has a single component.

@check(message="record component assignment",
       category="Style", subcategory="Programming Practice")
fun record_component_assignments(node) =
    node is AssignStmt(
        f_dest is DottedName(
            f_suffix is Identifier(
                p_referenced_decl() is ComponentDecl(
                    p_semantic_parent() is type@TypeDecl))))
    when {
        val num_comps = (from type.f_type_def select DefiningName).length;
        num_comps > 1 and
        unique([s.f_dest.f_suffix.text
                for s in (from node.parent select s@AssignStmt(
                    f_dest is DottedName(
                        f_suffix is Identifier(p_referenced_decl() is
                                               c2@ComponentDecl)
                        when type == c2.p_semantic_parent()),
                    any prev_siblings is p@AssignStmt when p == node))])
        .length == num_comps
    }
