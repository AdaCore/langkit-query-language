# Flag each generic actual parameter corresponding to a generic formal
# parameter with a default initialization, if positional notation is used.

import stdlib

fun check_generic(n, node) =
    n?.p_referenced_decl()?.f_formal_part?.f_decls[stdlib.param_pos(node)] is
    GenericFormal(f_decl is d@(FormalSubpDecl or ObjectDecl)
                  when d?.f_default_expr != null)

@check(message="use named notation when passing actual to defaulted generic parameter")
fun positional_actuals_for_defaulted_generic_parameters(node) =
    node is ParamAssoc(f_designator is null, parent is l@AssocList)
    when match l?.parent
          | p@GenericSubpInstantiation    =>
              check_generic(p?.f_generic_subp_name, node)
          | p@GenericPackageInstantiation =>
              check_generic(p?.f_generic_pkg_name, node)
          | *                             => false