# Flag each occurrence of a membership test of the form:
#   X in Subtype_Of_X
#   X in Subtype_Of_X'First .. Subtype_Of_X'Last

fun type_of(name) =
    if name.p_referenced_decl().p_type_expression() is t@TypeExpr
    then t.p_designated_type_decl()
    else null

@check(message="membership test instead of 'Valid",
       category="Style", subcategory="Portability")
fun membership_for_validity(node) =
    node is MembershipExpr(f_expr is var@Name)
    when not node.f_membership_exprs[2]
     and node.f_membership_exprs[1] is
         (typ@Name when type_of(var) == typ.p_referenced_decl()) or
         BinOp(f_op is OpDoubleDot,
               f_left is at1@AttributeRef
                         when at1.f_attribute.p_name_is("First")
                          and at1.f_prefix is typ1@Name,
               f_right is at2@AttributeRef
                          when at2.f_attribute.p_name_is("Last")
                           and at2.f_prefix is typ2@Name)
         when typ1.p_referenced_decl() == typ2.p_referenced_decl()
          and type_of(var) == typ1.p_referenced_decl()
