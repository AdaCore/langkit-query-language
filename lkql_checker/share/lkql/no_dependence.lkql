import stdlib

@check(category="Style", subcategory="Readability", message="Forbidden dependency")
fun no_dependence(node, unit_names=[]) =
    |" Flag every explicit dependency (with clause) to any of the library units
    |" designated by names passed as parameters.
    |"
    |" This rule has the following optional parameter for the ``+R`` option and for
    |" LKQL rule options files:
    |"
    |" *Unit_Names: list[string]*
    |"    List of fully qualified names designating the library units that
    |"    should not be explicitly depended upon.
    |"
    |" The list of unit names is case insensitive. Any case can be used both in
    |" the parameter or in the code's with clauses.
    |"
    |" .. rubric:: Example
    |"
    |" .. code-block:: ada
    |"    :emphasize-lines: 2
    |"
    |"    --  if the rule is activated as +RNo_Dependence:Unchecked_Conversion
    |"    with Unchecked_Conversion; -- FLAG
    |"
    |"    package Foo is
    |"    end Foo;
    node is n@Name(
        any parent(depth=2): WithClause
    ) when stdlib.any([name.to_lower_case == n.text.to_lower_case for name in unit_names])
