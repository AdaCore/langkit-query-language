# Flag any subprogram declaration, subprogram body declaration, expression
# function declaration, null procedure declaration, subprogram body stub or
# generic subprogram declaration if the corresponding subprogram has more than
# N formal parameters, where N is a parameter of the rule.
# A subprogram body, an expression function, a null procedure or a subprogram
# body stub is flagged only if there is no separate declaration for this
# subprogram. Subprogram renaming declarations and subprogram instantiations,
# as well as declarations inside expanded generic instantiations are never
# flagged.

fun num_params(node) =
    |" Return the number of parameters of a subprogram node
    (from node.f_subp_spec?.f_subp_params?.f_params select DefiningName).length

@unit_check(help="maximum number of subprogram parameters")
fun maximum_parameters(unit, n: int = 3) = [
    {message: "too many formal parameters (" & img(num_params(n)) & ")",
     loc: n.p_defining_name()}
    for n in from unit.root select
    node@(SubpBody or ExprFunction or NullSubpDecl or SubpBodyStub or
          ClassicSubpDecl or GenericSubpInternal)
    when (node is (ClassicSubpDecl or GenericSubpInternal) or
          node.p_previous_part() == null)
     and num_params(node) > n]
