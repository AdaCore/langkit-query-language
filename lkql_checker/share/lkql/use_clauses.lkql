# Flag names mentioned in use clauses. Use type clauses and names mentioned in
# them are not flagged.
# This rule has an optional parameter Exempt_Operator_Packages: do not flag a
# package name in a package use clause if it refers to a package that only
# declares operators in its visible part.

fun is_operator(s) =
    s is (BasicSubpDecl or BaseSubpBody)
    when s.p_defining_name().p_is_operator_name()

@check(message="use clause")
fun use_clauses(node, exempt_operator_packages=false) =
    node is Name(any parent(depth=2) is UsePackageClause)
    when (not exempt_operator_packages)
      or [s for s in match node.p_referenced_decl()
            | p@BasePackageDecl     => p.f_public_part.f_decls.children
            | p@PackageRenamingDecl => p.p_final_renamed_package().
                                       f_public_part.f_decls.children
            | GenericPackageInstantiation => p.p_designated_generic_decl().
                                             f_package_decl.f_public_part.
                                             f_decls.children
            | *                     => []
          if not is_operator(s)]?[1] != ()
