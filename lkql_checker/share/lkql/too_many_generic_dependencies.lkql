# Flag each generic unit in a with clause that depends on a chain of more
# than N generic library units (including itself).
# This rule has the N parameter: Positive integer specifying the maximal
# number of dependencies.

fun check_deps(node, n : int) =
    |" Return true if node has a chain of at least n generic units withed
    n == 0 or
    node is LibraryItem(
        parent is unit@CompilationUnit
        when [x for x in concat(
                  [[p for p in w.f_packages.children
                      if p.p_referenced_decl() is GenericDecl].to_list
                   for w in (from unit.f_prelude select WithClause)].to_list)
                if check_deps(x.p_referenced_decl()?.parent, n - 1)])

@unit_check(help="too many generic dependencies", remediation="MAJOR",
            category="Style", subcategory="Program Structure")
fun too_many_generic_dependencies(unit, n : int = 3) =
    if unit.root is CompilationUnit then
        [{message: "too many generic dependencies", loc: x}
         for x in
         concat([[p for p in w.f_packages.children
                    if p.p_referenced_decl() is decl@GenericDecl
                       when check_deps(decl.parent, n)].to_list
                 for w in from unit.root.f_prelude select WithClause].to_list)]
    else []
