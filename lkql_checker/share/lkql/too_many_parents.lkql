# Flag any tagged type declaration, interface type declaration, single task
# declaration or single protected declaration that has more than n
# parents. A parent here is either a (sub)type denoted by the subtype mark from
# the parent_subtype_indication (in case of a derived type declaration), or any
# of the progenitors from the interface list (if any).
# This rule has the following parameter: n: Positive integer
# specifying the maximal allowed number of parents/progenitors.

import stdlib

fun num_interfaces(n) =
    |" Return the number of interfaces for a SingleProtectedDecl, 0 otherwise
    |" since for all type declarations, super_types takes interfaces into
    |" account.
    if n is SingleProtectedDecl then n.f_interfaces.children_count else 0

@check(message="type has too many parents", remediation="MAJOR",
       category="Style", subcategory="Object Orientation")
fun too_many_parents(node, n : int = 5) =
    node is (TypeDecl(p_is_tagged_type(): true) |
             TaskTypeDecl | ProtectedTypeDecl | SingleProtectedDecl)
    when ({
        val l = num_interfaces(node);
        val st = stdlib.super_types(node, max_depth=n - l + 1);
        l + st.length > n
    })
