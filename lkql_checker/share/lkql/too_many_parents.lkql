# Flag any tagged type declaration, interface type declaration, single task
# declaration or single protected declaration that has more than maxParents
# parents. A parent here is either a (sub)type denoted by the subtype mark from
# the parent_subtype_indication (in case of a derived type declaration), or any
# of the progenitors from the interface list (if any).
# This rule has the following parameter: n: Positive integer
# specifying the maximal allowed number of parents/progenitors.

# TODO: [U317-058] Use a dedicated length operator on is_list_type Nodes
fun length(l) = {
    fun internal(l, n) =
        if l[n + 1] != null then internal(l, n+1) else n;
    internal(l, 0)
}

fun num_interfaces(n) = match n
    | TaskTypeDecl        => length(n.f_definition?.f_interfaces)
    | ProtectedTypeDecl   => length(n.f_interfaces)
    | SingleProtectedDecl => length(n.f_interfaces)
    | *                   => 0

@check(message="too many parents")
fun too_many_parents(node, n : int = 5) =
    node is BasicDecl
    when (match node
          | TypeDecl            => node.p_is_tagged_type()
          | TaskTypeDecl        => true
          | ProtectedTypeDecl   => true
          | SingleProtectedDecl => true
          | *                   => false)
    and ({
        val l = num_interfaces(node);
        l+super_types(node, depth=n-l+1).length > n
    })
