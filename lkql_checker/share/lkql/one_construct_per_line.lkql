# Flag any statement, declaration or representation clause if the code
# line where this construct starts contains some other Ada code symbols
# preceding or following this construct. The following constructs are not
# flagged:
# - enumeration literal specification;
# - parameter specifications;
# - discriminant specifications;
# - loop parameter specification;
# - entry index specification;

import stdlib

@check(message="more than one construct on the same line", remediation="EASY",
       category="Style", subcategory="Readability")
fun one_construct_per_line(node) =
    # Flag any statement, declaration or representation clause
    node is (Stmt | BasicDecl | AttributeDefClause |
             EnumRepClause | RecordRepClause | AtClause)
    # except for enum literal, param spec, discriminant spec
    when (not node is (EnumLiteralDecl | ParamSpec | DiscriminantSpec |
                       # or loop param or entry index.
                       ForLoopVarDecl | EntryIndexSpec |
                       # Also ignore anonymous or nested constructs
                       # generating false positives.
                       SingleTaskTypeDecl | AnonymousTypeDecl |
                       LabelDecl | GenericSubpInternal |
                       ConcreteFormalSubpDecl |
                       ExtendedReturnStmtObjectDecl |
                       NamedStmtDecl))
     and (node.token_end().end_line ==
          stdlib.next_non_blank_token_line(node.token_end())
          or node.token_start().start_line ==
             stdlib.previous_non_blank_token_line(node.token_start()))
