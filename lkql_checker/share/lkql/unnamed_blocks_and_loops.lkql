# Flag each unnamed block statement. Flag a unnamed loop statement if this
# statement is enclosed by another loop statement or if it encloses another
# loop statement.

@unit_check(help="compound statements naming", remediation="EASY",
       category="Style", subcategory="Programming Practice")
fun unnamed_blocks_and_loops(unit) = [
    {message: "non-named " &
              (if n is BlockStmt then "block"
               else (if n is BaseLoopStmt(any children: BaseLoopStmt)
                     then "nesting " else "nested ") & "loop") & " statement",
     loc: n}
    for n in from unit.root select node@CompositeStmt
    when (not node.parent is NamedStmt)
     and node is (BlockStmt |
                 BaseLoopStmt (any children: BaseLoopStmt) |
                 BaseLoopStmt (any parent: BaseLoopStmt))]
