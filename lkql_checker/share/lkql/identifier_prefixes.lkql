# Flag each defining identifier that does not have a prefix corresponding to
# the kind of declaration it is defined by. The defining names in the following
# kinds of declarations are checked:
# - type and subtype declarations (task, protected and access types are treated
#   separately);
# - enumeration literal specifications (not including character literals) and
#   function renaming declarations if the renaming entity is an enumeration
#   literal;
# - exception declarations and exception renaming declarations;
# - constant and number declarations (including object renaming declarations if
#   the renamed object is a constant).
#
# Defining names declared by single task declarations or single protected
# declarations are not checked by this rule.
# The defining name from the full type declaration corresponding to a private
# type declaration or a private extension declaration is never flagged. A
# defining name from an incomplete type declaration is never flagged.
# The defining name from a subprogram renaming-as-body declaration is never
# flagged.
# For a deferred constant, the defining name in the corresponding full constant
# declaration is never flagged.
# The defining name from a body that is a completion of a program unit
# declaration or a proper body of a subunit is never flagged.
# The defining name from a body stub that is a completion of a program unit
# declaration is never flagged.
#
# Note that the rule checks only defining names. Usage name occurrence are not
# checked and are never flagged.
#
# The rule may have the following parameters:
# - Type=string
#   Specifies the prefix for a type or subtype name.
# - Concurrent=string
#   Specifies the prefix for a task and protected type/subtype name. If this
#   parameter is set, it overrides for task and protected types the prefix set
#   by the Type parameter.
# - Access=string
#   Specifies the prefix for an access type/subtype name. If this parameter is
#   set, it overrides for access types the prefix set by the Type parameter.
# - Class_Access=string
#   Specifies the prefix for the name of an access type/subtype that points to
#   some class-wide type. If this parameter is set, it overrides for such
#   access types and subtypes the prefix set by the Type or Access parameter.
# - Subprogram_Access=string
#   Specifies the prefix for the name of an access type/subtype that points to
#   a subprogram. If this parameter is set, it overrides for such access
#   types/subtypes the prefix set by the Type or Access parameter.
# - Derived_Ancestor=string1
# - Derived=string2
#   Specifies the prefix for a type that is directly derived from a given type
#   or from a subtype thereof. string1 should be a full expanded Ada name of
#   the ancestor type (starting from the full expanded compilation unit name),
#   string2 defines the prefix to check. If this parameter is set, it overrides
#   for types that are directly derived from the given type the prefix set by
#   the Type parameter.
# - Constant=string
#   Specifies the prefix for defining names from constants and named number
#   declarations, including the object renaming declaration if the renamed
#   object is a constant
# - Enum=string
#   Specifies the prefix for defining enumeration literals and for the defining
#   names in a function renaming declarations if the renamed entity is an
#   enumeration literal.
# - Exception=string
#   Specifies the prefix for defining names from exception declarations and
#   exception renaming declarations.
# - Exclusive
#   Check that only those kinds of names for which specific prefix is defined
#   have that prefix (e.g., only type/subtype names have prefix T_, but not
#   variable or package names), and flag all defining names that have any of
#   the specified prefixes but do not belong to the kind of entities this
#   prefix is defined for.
#
# All checks for name prefixes are case-sensitive

import stdlib

@unit_check(help="prefixes in defining names", remediation="EASY",
            parametric_exemption=true)
fun identifier_prefixes(unit,
                        type="-",
                        concurrent="-",
                        access="-",
                        class_access="-",
                        subprogram_access="-",
                        derived="-", derived_ancestor="",
                        constant="-",
                        exception="-",
                        enum="-",
                        exclusive=true) = {
    fun check_exclusive(str, exclusive,
                        type_exclusive=true, concurrent_exclusive=true,
                        access_exclusive=true, class_access_exclusive=true,
                        subprogram_access_exclusive=true,
                        derived_exclusive=true, constant_exclusive=true,
                        exception_exclusive=true, enum_exclusive=true) =
        exclusive and
        ((type_exclusive and str.starts_with(type)) or
         (concurrent_exclusive and str.starts_with(concurrent)) or
         (access_exclusive and str.starts_with(access)) or
         (class_access_exclusive and str.starts_with(class_access)) or
         (subprogram_access_exclusive and str.starts_with(subprogram_access)) or
         (derived_exclusive and str.starts_with(derived)) or
         (constant_exclusive and str.starts_with(constant)) or
         (exception_exclusive and str.starts_with(exception)) or
         (enum_exclusive and str.starts_with(enum)));

    fun check_enum(str) =
        (enum != "-" and not str.starts_with(enum)) or
        check_exclusive(str, exclusive=exclusive, enum_exclusive=false);

    val str_prefix = " does not start with prefix ";

    [{message: n.text &
      (match n.parent
       | p@(TaskTypeDecl or ProtectedTypeDecl or
            SubtypeDecl(p_canonical_type() is TaskTypeDecl or ProtectedTypeDecl))
         when p is not SingleTaskTypeDecl and concurrent != "-"
       => str_prefix & concurrent
       | (TypeDecl(f_type_def is TypeAccessDef(f_subtype_indication is *(f_name is
          AttributeRef(f_attribute is Identifier(p_name_is("Class") is true))))) or
          SubtypeDecl(p_canonical_type() is TypeDecl(f_type_def is
                      TypeAccessDef(f_subtype_indication is *(f_name is
          AttributeRef(f_attribute is Identifier(p_name_is("Class") is true)))))))
         when class_access != "-" => str_prefix & class_access
       | (TypeDecl(f_type_def is AccessToSubpDef) or
          SubtypeDecl(p_canonical_type() is
                      TypeDecl(f_type_def is AccessToSubpDef)))
         when subprogram_access != "-" => str_prefix & subprogram_access
       | (TypeDecl(f_type_def is AccessDef) or
          SubtypeDecl(p_canonical_type() is TypeDecl(f_type_def is AccessDef)))
         when access != "-" => str_prefix & access

       | p@(TypeDecl(f_type_def is DerivedTypeDef) or SubtypeDecl)
         when derived != "-" and
              p.p_canonical_type().p_base_type() is t@BaseTypeDecl
              when t.p_canonical_type().p_canonical_fully_qualified_name() ==
                   derived_ancestor
         => str_prefix & derived
       | BaseTypeDecl    => str_prefix & type
       | EnumLiteralDecl => str_prefix & enum
       | p@* => match p.parent
                | ObjectDecl or NumberDecl => str_prefix & constant
                | SubpRenamingDecl         => str_prefix & enum
                | ExceptionDecl            => str_prefix & exception
                | * => " does not have an exclusive prefix"),
      loc: n}
    for n in from unit.root select node@DefiningName when match node.parent
    # Concurrent types
    | p@(TaskTypeDecl or ProtectedTypeDecl or TaskBody or ProtectedBody or
         SubtypeDecl(p_canonical_type() is TaskTypeDecl or ProtectedTypeDecl))
      when p is not SingleTaskTypeDecl and concurrent != "-"
    =>
        p.p_previous_part() is (null or IncompleteTypeDecl) and
        ((not node.f_name.text.starts_with(concurrent)) or
         check_exclusive(node.f_name.text,
                         exclusive=exclusive,
                         concurrent_exclusive=false))

    # 'Class access
    | (p@TypeDecl(f_type_def is TypeAccessDef(f_subtype_indication is *(f_name is
       AttributeRef(f_attribute is Identifier(p_name_is("Class") is true))))) or
       p@SubtypeDecl(p_canonical_type() is TypeDecl(f_type_def is
                     TypeAccessDef(f_subtype_indication is *(f_name is
       AttributeRef(f_attribute is Identifier(p_name_is("Class") is true)))))))
      when class_access != "-"
    =>
        p?.p_previous_part() is (null or IncompleteTypeDecl) and
        ((not node.f_name.text.starts_with(class_access)) or
         check_exclusive(node.f_name.text,
                         exclusive=exclusive,
                         class_access_exclusive=false))

    # Subprogram access
    | (p@TypeDecl(f_type_def is AccessToSubpDef) or
       p@SubtypeDecl(p_canonical_type() is
                     TypeDecl(f_type_def is AccessToSubpDef)))
      when subprogram_access != "-"
    =>
        p?.p_previous_part() is (null or IncompleteTypeDecl) and
        ((not node.f_name?.f_name.text.starts_with(subprogram_access)) or
         check_exclusive(node.f_name?.f_name.text,
                         exclusive=exclusive,
                         subprogram_access_exclusive=false))

    # Other access types
    | (p@TypeDecl(f_type_def is AccessDef) or
       p@SubtypeDecl(p_canonical_type() is TypeDecl(f_type_def is AccessDef)))
      when access != "-"
    =>
        p?.p_previous_part() is (null or IncompleteTypeDecl) and
        ((not node.f_name?.f_name.text.starts_with(access)) or
         check_exclusive(node.f_name?.f_name.text,
                         exclusive=exclusive,
                         access_exclusive=false))

    # (Sub)Types derived from derived_ancestor
    | p@(TypeDecl(f_type_def is DerivedTypeDef) or SubtypeDecl)
      when derived != "-" and
           p.p_canonical_type().p_base_type() is t@BaseTypeDecl
           when t.p_canonical_type().p_canonical_fully_qualified_name() ==
                derived_ancestor
    =>
        p.p_previous_part() is (null or IncompleteTypeDecl) and
        ((not node.f_name.text.starts_with(derived)) or
         check_exclusive(node.f_name.text,
                         exclusive=exclusive,
                         derived_exclusive=false))

    # Exclude IncompleteTypeDecl
    | IncompleteTypeDecl => false

    # Other types and subtypes
    | p@BaseTypeDecl when p is not SingleTaskTypeDecl =>
        p.p_previous_part() is (null or IncompleteTypeDecl) and
        ((type != "-" and not node.f_name.text.starts_with(type)) or
         check_exclusive(node.f_name.text,
                         exclusive=exclusive,
                         type_exclusive=false))

    # Enums
    | EnumLiteralDecl => check_enum(node.f_name.text)

    # Look one level up for remaining cases
    | p@* => match p?.parent
        # Constants
        | (o@ObjectDecl
           when (o?.f_has_constant?.p_as_bool() or
                 # Renaming of a constant
                 (o?.f_renaming_clause != null and
                  stdlib.ultimate_alias(o?.f_renaming_clause?.f_renamed_object) is
                  ObjectDecl(f_has_constant is ConstantPresent)))
            and o?.p_public_part_decl() == null)
          or NumberDecl
        =>
            (constant != "-" and not node.f_name.text.starts_with(constant)) or
            check_exclusive(node.f_name.text,
                            exclusive=exclusive,
                            constant_exclusive=false)


        # Function renaming an enum literal
        | r@SubpRenamingDecl
          when r?.f_renames?.f_renamed_object?.p_referenced_decl() is EnumLiteralDecl
        => check_enum(node.f_name.text)

        # Exceptions
        | ExceptionDecl =>
            (exception != "-" and not node.f_name.text.starts_with(exception)) or
            check_exclusive(node.f_name.text,
                            exclusive=exclusive,
                            exception_exclusive=false)

        # Check all other defining names for exclusion except for completions
        # and renaming-as-body
        | p2@* =>
            ((not p2 is (Body or SubpRenamingDecl)) or
             p2?.p_previous_part() == null) and
            ((not p2 is ObjectDecl) or p2?.p_public_part_decl() == null) and
            check_exclusive(node.f_name.text, exclusive=exclusive)]
}
