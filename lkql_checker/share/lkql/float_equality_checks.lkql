# Flag all explicit calls to the predefined equality operations for
# floating-point types and private types whose completions are floating-point
# types. Both '=' and '/=' operations are checked. User-defined equality
# operations are not flagged, nor are uses of operators that are renamings of
# the predefined equality operations. Also, the '=' and '/=' operations for
# fixed-point types are not flagged.

fun is_float(n) = n.p_expression_type().p_full_view().p_is_float_type()

@check(message="use of equality operation for float values")
fun float_equality_checks(node) =
    node is (RelationOp(f_op is op@(OpEq or OpNeq))
             when op.p_referenced_decl() == null and is_float(node.f_left))
    or CallExpr
       when (node.f_name.p_name_is("\"=\"") or
             node.f_name.p_name_is("\"/=\""))
        and node.p_referenced_decl() == null
        and is_float(node.f_suffix[1].f_r_expr)
