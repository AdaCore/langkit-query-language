@check(message="Ada 2005 formal package declaration",
       category="Style", subcategory="Programming Practice")
fun ada05_formal_packages(node) =
    |" Flag formal package declarations that are not allowed in Ada 95. Ada 95 allows
    |" the box symbol ``(<>)`` to be used alone as a whole formal package actual
    |" part only.
    |"
    |" This rule has no parameters.
    |"
    |" .. rubric:: Example
    |"
    |" .. code-block:: ada
    |"    :emphasize-lines: 2
    |"
    |"    generic
    |"       with package NP is new P (T => <>);  --  FLAG
    |"    package Pack_G is
    node is GenericPackageInstantiation(parent: GenericFormalPackage)
    when {
        val num_box = [p for p in node.f_params.children
                       if p.f_r_expr is BoxExpr].length;
        num_box > 1 or (num_box == 1 and (node.f_params.children_count > 1 or
                                          node.f_params[1].f_designator != null))
    }
