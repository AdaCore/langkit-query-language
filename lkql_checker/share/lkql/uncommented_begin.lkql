# Flags each body with declarations and a statement part that does not
# include a trailing comment on the line containing the begin keyword; this
# trailing comment needs to specify the entity name and nothing else. The
# begin is not flagged if the body does not contain any declarations.

import stdlib

@unit_check(help="BEGIN not marked with entity name comment",
            remediation="EASY", category="Style", subcategory="Readability")
fun uncommented_begin(unit) = [
    {message: "mark BEGIN with -- " & n.parent.p_defining_name().text,
     loc: n.token_start().previous(exclude_trivia=true)}
    for n in from unit.root
    select node@HandledStmts(parent: p@(PackageBody | SubpBody |
                                          EntryBody | ProtectedBody |
                                          TaskBody))
    when p.f_decls.f_decls is l
         when l[1]
          and not stdlib.find_comment(node.token_start().previous(),
                                      p.p_defining_name().text)
]
