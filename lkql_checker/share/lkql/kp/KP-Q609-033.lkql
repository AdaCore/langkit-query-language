# Flag local objects containing a component of type
# Ada.Real_Time.Timing_Events.Timing_Event if restriction
# No_Local_Timing_Events is in effect.

import stdlib

@memoized
fun has_no_local_timing_events_restriction(unit) =
    from unit.p_config_pragmas("Restrictions")
    select first p@PragmaNode
    when (from p.f_args select first n@Name
          when n.p_name_is("No_Local_Timing_Events"))

@check(help="possible occurrence of KP Q609-033 - global analysis required",
       message="possible occurrence of KP Q609-033",
       impact="7.[123].*,7.4.[1-3].*")
fun kp_q609_033(node) =
    node is ObjectDecl
    when has_no_local_timing_events_restriction(node.p_enclosing_compilation_unit())
     and stdlib.has_local_scope(node)
     and node.f_type_expr.p_designated_type_decl() is BaseTypeDecl(
             any stdlib.component_types is type@TypeDecl
             when type.p_fully_qualified_name() ==
                  "Ada.Real_Time.Timing_Events.Timing_Event")
