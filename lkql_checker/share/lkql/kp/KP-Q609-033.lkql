# Flag local objects containing a component of type
# Ada.Real_Time.Timing_Events.Timing_Event if restriction
# No_Local_Timing_Events is in effect.

import stdlib

@memoized
fun has_no_local_timing_events_restriction() =
    select first p@PragmaNode
    when p.f_id.p_name_is("Restrictions")
     and from p.f_args select first n@Name
         when n.p_name_is("No_Local_Timing_Events")

@check(message="possible occurrence of KP Q609-033",
       impact="7.[123].*,7.4.[1-3].*")
fun kp_q609_033(node) =
    node is ObjectDecl
    when has_no_local_timing_events_restriction()
     and stdlib.has_local_scope(node)
     and node.f_type_expr.p_designated_type_decl() is BaseTypeDecl(
             any stdlib.component_types is type@TypeDecl
             when type.p_fully_qualified_name() ==
                  "Ada.Real_Time.Timing_Events.Timing_Event")
