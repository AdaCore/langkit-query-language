import stdlib

fun is_int_attr(expr) =
    |" Returns whether the given expression is a universal-integer valued
    |" attribute reference.
    expr is AttributeRef
    when expr.p_expression_type() == expr.p_universal_int_type()

fun is_subject_to_constraint(name) =
    |" Returns whether the given name refers to a subtype subject to a
    |" constraint.
    {
        val decl = match name
           | DefiningName => name.p_basic_decl()
           | Name => name.p_referenced_decl();

        match decl
        | (ComponentDef | DiscriminantSpec | ObjectDecl | ParamSpec) =>
            decl.f_type_expr is SubtypeIndication
            and stdlib.is_subtype_indication_constrained(decl.f_type_expr)
        | SubtypeDecl => stdlib.is_subtype_indication_constrained(decl.f_subtype)
    }


@check(help="possible occurrence of KP 19501",
       message="possible occurrence of KP 19501")
fun kp_19501(node) =
    |" Flag constructions involving an integer valued attribute reference when
    |" the attribute reference is:
    |" * an actual parameter in a call where the subtype of the corresponding
    |"   formal parameter is subject to a constraint
    |" * the expression of an assignment where the subtype of the target object
    |"   is subject to a constraint
    |" * the operand of a qualified expression where the subtype mark
    |"   denotes a subtype that is subject to a constraint
    |" * an array index value in an indexed component name
    match node
    | CallExpr(p_is_call(): true) =>
        stdlib.any([
            is_int_attr(p.actual) and is_subject_to_constraint(p.param)
            for p in node.p_call_params()
        ])
    | AssignStmt =>
        is_int_attr(node.f_expr) and is_subject_to_constraint(node.f_dest)
    | QualExpr(f_suffix: ParenExpr(f_expr: operand)) =>
        is_int_attr(operand)
        and node.f_prefix is Name
        and is_subject_to_constraint(node.f_prefix)
    | CallExpr(p_kind(): "array_index") =>
        stdlib.any([
            is_int_attr(p.f_r_expr)
            for p in node.f_suffix.children
        ])
