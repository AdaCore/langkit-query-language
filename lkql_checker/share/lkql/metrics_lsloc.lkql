# The Metrics_LSLOC rule takes a positive integer as upper bound. A program
# unit declaration or a program unit body exceeding this limit will be flagged.
#
# The metric counts the total number of declarations and the total number of
# statements.
#
# This rule has the following optional parameter:
#   Subprograms: Check for subprogram bodies only.

import metrics

@unit_check(help="Logical source lines", execution_cost=3)
fun metrics_lsloc(unit, n : int = 5, subprograms=false) = {
    val nodes =
        if subprograms
        then (from unit.root select BaseSubpBody)
        else (from unit.root select
              (GenericPackageDecl | PackageDecl | PackageBody |
              BaseSubpBody |
              TaskTypeDecl | SingleTaskDecl | TaskBody |
              SingleProtectedDecl | ProtectedTypeDecl | ProtectedBody));

    [
        { message: "LSLOC is too high: " & img(t[2]), loc: t[1] }
        for t in [(body, metrics.logical_slocs(body)) for body in nodes]
        if t[2] > n
    ]
}
