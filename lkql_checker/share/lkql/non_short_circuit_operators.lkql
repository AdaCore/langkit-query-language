# Flag all calls to predefined "and" and "or" operators for any boolean type.
# Calls to user-defined "and" and "or" and to operators defined by renaming
# declarations are not flagged. Calls to predefined "and" and "or" operators
# for modular types or boolean array types are not flagged.
# This rule has the parameter Except_Assertions: Do not flag operators that are
# subcomponents of the assertion-related pragmas or aspects.

import stdlib

fun operator_image(node) =
    |" Return the string of a given AND/OR operator
    match node
    | OpAnd      => "AND"
    | OpOr       => "OR"
    | c@CallExpr => (if c.p_relative_name()?.p_name_is("\"and\"")
                     then "AND" else "OR")
    | *          => "unknown"

@unit_check(help="use of predefined AND and OR for boolean types",
            remediation="EASY")
fun non_short_circuit_operators(unit, except_assertions=false) = [
    {message: "use of predefined " & operator_image(n) & " for boolean type",
     loc: n}
    for n in from unit.root select node@(((OpAnd or OpOr)
              when node.p_referenced_decl() == null
               and stdlib.is_standard_boolean(node.parent)
               and stdlib.is_standard_boolean(node.parent?.f_left))
     or CallExpr(p_relative_name() is n@Name
                 when n?.p_name_is("\"and\"") or n?.p_name_is("\"or\""))
        when node.p_referenced_decl() == null
         and stdlib.is_standard_boolean(node)
         and stdlib.is_standard_boolean(node.f_suffix[1]?.f_r_expr))
    when not (except_assertions and stdlib.within_assert(node))]
