# Flag instantiations of Ada.Unchecked_Conversion if the actual for the formal
# type Source is the System.Address type (or a type derived from it), and the
# actual for the formal type Target is an access type (including types derived
# from access types). This includes cases when the actual for Source is a
# private type and its full declaration is a type derived from System.Address,
# and cases when the actual for Target is a private type and its full
# declaration is an access type. The rule is checked inside expanded generics.

import stdlib

fun check_uc_params(source, target) =
    |" Check that source is derived from System.Address and target is an
    |" access type.
    # TODO: replace p_full_view() by new LAL API once U915-027 is implemented
    target.f_r_expr.p_referenced_decl().p_full_view().p_is_access_type() and
    source.f_r_expr.p_referenced_decl().p_root_type().p_full_view().
    p_root_type().p_fully_qualified_name() == "System.Address"

@check(message="address-to-access conversion",
       follow_generic_instantiations=true)
fun unchecked_address_conversions(node) =
    node is GenericSubpInstantiation
    when ({
        val name = stdlib.ultimate_subprogram_alias(node.p_designated_subp()).
                   p_fully_qualified_name();
        name == "Ada.Unchecked_Conversion" or name == "Unchecked_Conversion"})
     and node.f_params is params@AssocList
         when params.children_count == 2
          and if params[1].f_designator == null or
                 params[1].f_designator.p_name_is("source") 
              then check_uc_params(params[1], params[2])
              else check_uc_params(params[2], params[1])
