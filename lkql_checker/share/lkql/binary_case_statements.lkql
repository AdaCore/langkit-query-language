# Flag a case statement if this statement has only two alternatives, one
# containing exactly one choice, the other containing exactly one choice
# or the `OTHERS' choice.
# This rule has an optional parameter Except_Enums: exclude case statements on
# enumerated types.

fun to_if_stmt(case_stmt) =
    |" This function turns the given "case" statement into a if statement.
    |" Assumes that the "case" has only 2 alternatives.
    {
        val alts = case_stmt.f_alternatives.children;
        val cond = new RelationOp(
            f_left=case_stmt.f_expr,
            f_op=new OpEq(),
            f_right=alts[1].f_choices[1]
        );
        match alts[2].f_choices[1]
        | OthersDesignator =>
            new IfStmt(
                f_cond_expr=cond,
                f_then_stmts=alts[1].f_stmts,
                f_alternatives=new ElsifStmtPartList(),
                f_else_stmts=alts[2].f_stmts
            )
        | choice           =>
            new IfStmt(
                f_cond_expr=cond,
                f_then_stmts=alts[1].f_stmts,
                f_alternatives=new ElsifStmtPartList([
                    new ElsifStmtPart(
                        f_cond_expr=new RelationOp(
                            f_left=case_stmt.f_expr,
                            f_op=new OpEq(),
                            f_right=choice
                        ),
                        f_stmts=alts[2].f_stmts
                    )
                ]),
                f_else_stmts=new StmtList()
            )
    }

@check(message="CASE statement can be replaced with IF statement",
       category="Style", subcategory="Programming Practice",
       auto_fix=(n, ctx) => ctx.replace(n, to_if_stmt(n)))
fun binary_case_statements(node, except_enums = false) =
    node is CaseStmt(
        f_alternatives: CaseStmtAlternativeList(
            all children(depth=1): a@CaseStmtAlternative
            when a.f_choices.children_count == 1
        )
        when node.f_alternatives.children_count == 2
    ) and not (except_enums and node.f_expr.p_expression_type().p_is_enum_type())
