# Flag each generic package instantiation when it can be determined that
# it has the same actual parameters as another generic instantiation.
# This determination is conservative and will check for a match on:
# - integer, character and string literals
# - identifier names other than function calls (e.g. types, variables,
#   subprogram names).
# Note that no verification is made to check if the instantiation declares
# global variables (if they do, removing the flagged duplicates will likely not
# be possible).

import stdlib

fun same_exprs(e1, e2) =
    match e1
    | IntLiteral    => e2 is IntLiteral and
                       e1.p_denoted_value() == e2.p_denoted_value()
    | CharLiteral   => e2 is CharLiteral and
                       e1.p_denoted_value() == e2.p_denoted_value()
    | StringLiteral => e2 is StringLiteral and
                       e1.p_denoted_value() == e2.p_denoted_value()
    | RealLiteral   => false
    | Name          => e2 is Name and
                       e1.p_referenced_decl() == e2.p_referenced_decl() and
                       not e1.p_is_call()
    | *             => false

fun params_equal(l1, l2, i: int) =
    i == 0 or
    (same_exprs(l1[i].actual, l2[i].actual) and params_equal(l1, l2, i - 1))

fun same_params(i1, i2) = {
    val params1 = i1.p_inst_params();
    val params2 = i2.p_inst_params();
    val length = params1.length;
    length == params2.length and params_equal(params1, params2, length)
}

@memoized
fun insts() = select GenericPackageInstantiation

@memoized
fun same_instance(node) =
    [i for i in insts()
       if i != node and
          node.f_generic_pkg_name.p_referenced_decl() ==
          i.f_generic_pkg_name.p_referenced_decl() and same_params(node, i)]?[1]

@unit_check(help="duplicate generic package instantiations",
            category="Style", subcategory="Program Structure")
fun same_instantiations(unit) =
    [{message: "same instantiation found at " &
               stdlib.sloc_image(same_instance(n)), loc: n}
     for n in from unit.root select GenericPackageInstantiation
              if same_instance(n)]
