# Flag each control structure whose nesting level exceeds the value provided in
# the rule parameter n.
# The control structures checked are the following:
# - if statement
# - case statement
# - loop statement
# - selective accept statement
# - timed entry call statement
# - conditional entry call statement
# - asynchronous select statement

# Return true if node and its parents contain more than n control structure
fun check_control_parent(node, n, level: int = 0) =
    if node == null then false
    else if node is (IfStmt or CaseStmt or BaseLoopStmt or SelectStmt)
    then (if level == n then true
         else check_control_parent(node?.parent, n, level + 1))
    else check_control_parent(node?.parent, n, level)

@check(message="nesting level of control structures too deep")
fun overly_nested_control_structures(node, n: int = 3) =
    node is (IfStmt or CaseStmt or BaseLoopStmt or SelectStmt)
    when check_control_parent(node?.parent, n)
